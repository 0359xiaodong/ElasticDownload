// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'maven'
apply plugin: 'signing'

version = elasticLibraryVersionName  // set in gradle.properties in project root
group = "com.github.tibolte"

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        pom.project {
            name 'ElasticDownload'
            packaging 'aar'
            description 'An animated download view with some elasticity for Android'
            url 'https://github.com/Tibolte/ElasticDownload'

            scm {
                url 'scm:git@github.com:Tibolte/ElasticDownload.git'
                connection 'scm:git@github.com:Tibolte/ElasticDownload.git'
                developerConnection 'scm:git@github.com:Tibolte/ElasticDownload.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'thibault.guegan'
                    name 'Thibault Gu√©gan'
                    email 'thibault.guegan@gmail.com'
                }
            }
        }
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}
